plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.0.13'
	id 'org.beryx.jlink' version '2.25.0'
	id 'com.diffplug.spotless' version '6.5.0'
}

repositories {
	mavenCentral()
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

dependencies {
	implementation 'com.google.code.gson:gson:2.10'
	implementation 'io.github.palexdev:materialfx:11.13.5'
	implementation 'org.apache.httpcomponents:httpclient:4.5.13'
	implementation 'org.slf4j:slf4j-nop:2.0.3'
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

application {
	mainModule = 'cowin'
	mainClass = 'cowin.CowinMain'
}

javafx {
	version = '17.0.2'
	modules = [
		'javafx.controls',
		'javafx.fxml'
	]
}

jlink {
	//noinspection GroovyAssignabilityCheck
	options = [
		'--strip-debug',
		'--compress',
		'2',
		'--no-header-files',
		'--no-man-pages'
	]
	launcher {
		name = 'app'
	}
}

jlinkZip {
	group = 'distribution'
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
	}
	freshmark {
		target '**/*.md'
	}
	format 'xml', {
		target '**/*.fxml'
		//noinspection GroovyAssignabilityCheck
		eclipseWtp('xml')
	}
	format 'css', {
		target '**/*.css'
		prettier()
	}
}

//noinspection ConfigurationAvoidance
task installPreCommitGitHook(type: Copy){
	from new File(rootProject.rootDir, 'scripts/pre-commit')
	into { new File(rootProject.rootDir, '.git/hooks')}
	fileMode 0775
}

build.dependsOn spotlessApply
build.dependsOn installPreCommitGitHook
